/*
	************************************************
		see disclaimer on end of file.
	************************************************
*/
/*!
 * @file bsec_iot_example.ino
 *
 * @brief
 * Example for using of BSEC library in a fixed configuration with the BME680 sensor.
 * This works by running an endless loop in the bsec_iot_loop() function.
 * Presets for ESP8266:
 *	C:\Users\<user>\Documents\Arduino\BSEC_1.4.5.1_Generic_Release_20171214\BSEC_1.4.5.1_Generic_Release_20171214\algo\bin\esp8266\libalgobsec.a
 *  into folder:  
 * 	C:\Users\<user>\AppData\Local\Arduino15\packages\esp8266\hardware\esp8266\2.4.0-rc1\tools\sdk\lib
 *  See BSEC doku, 
 *	Download: https://www.bosch-sensortec.com/bst/products/all_products/bsec
 *  ESP8266: "Wemos D1 Esp-Wroom-02 Motherboard ESP8266 Mini-WiFi NodeMCU Module ESP8266+18650 Battery+0.96 OLED" from ebay 
 */

/*!
 * @addtogroup bsec_examples BSEC Examples
 * @brief BSEC usage examples
 * @{*/

/*********************************************************************************************************************** 
 * Wireless sensor for measuring temperature, humidity, pressure, lightlevel and airquality.
 * Hardware: NodeMCU V1.0, RFM69CW, BME680, BH1750 and 1.3 inch 128x64 LCD.
 * Protokoll is CustomSensor.
 * Frequency is 868.3 MHz (or 433 MHz / 915 MHz based on RFM69CW model).
 * V1.3  10.11.2017, T.Hirte
 * V1.4  include BH1750 light sensor, 17.11.2017, T.Hirte
 * V1.5  umgestellt auf Bosch Sensortec Software vom 17.11.2017, 01.12.2017, T.Hirte
 * V1.6  adaptiert für Wemos D1 with integrated OLED + 18650 (Wroom2).  
 ***********************************************************************************************************************/

/***********************************************************************************************************************/
/* header files */
/***********************************************************************************************************************/

#include "bsec_integration.h"
#include <Wire.h>

#include "RFMxx.h"
#include "SensorBase.h" //calculate crc byte

#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#include <AS_BH1750.h>

/***********************************************************************************************************************/
/* variables */
/***********************************************************************************************************************/

SensorBase crc8;

/* Sensor Config */
#define VERSION               "V1.5"
#define NODEID                6        //every node needs his own unique ID 
#define BME680_I2C_ADDR       0x77     //BlueDot has 0x77, standard address is 0x76 

unsigned long DATA_RATE     = 17241ul; //default data rate (for transmit on RFM69)
unsigned long INITIAL_FREQ  = 868300;  //default frequency in kHz (5 kHz steps, 860480 ... 879515) 

float TEMP_CORR             = 2.3f;    //adjust temperature, 2.4f => -2.4 degrees !

bool DEBUG                  = 0;       //activate debug mode

uint8_t loop_count_lim      = 20;      //time to wait between data transmissions (20: 20 * 3sec = 60sec)
uint8_t loop_counter;

#define LEDpin LED_BUILTIN             //auto or set pin of your choice

/* RFM69, PIN-config SPI (GPIO XX): */

#ifdef ESP8266_NODEMCU_V1
	#define RFM_SS                15 //(D8)   SS pin -> RFM69 (NSS)
	#define RFM_MISO              12 //(D6) MISO pin <- RFM69 (MOSI) //only used by soft spi 
	#define RFM_MOSI              13 //(D7) MOSI pin -> RFM69 (MISO) //only used by soft spi
	#define RFM_SCK               14 //(D5)  SCK pin -> RFM69 (SCK)  //only used by soft spi
	#define RFM_IRQ                2 //(D4)  IRQ pin <- RFM69 (DIO0)
#else
	
#endif 
RFMxx rfm(RFM_MOSI, RFM_MISO, RFM_SCK, RFM_SS, RFM_IRQ);

/* OLED display */
bool OLED                   = 1;   //activate the OLED display (SH1106 or SSD1306)
#define OLED_ADR              0x3C //set I2C adress 0x3C or 0x3D

Adafruit_SSD1306 display;

/* Lichtsensor  */
#define BH1750_ADR            0x23  //set I2C adress 0x23 or 0x5C

AS_BH1750 bh1750(BH1750_ADR);

/*********************************************************************************************************************

  Hardware setup:                                                   BME680              RFM69CW
                         NodeMCU                                   +------+            +-------+
                         +--\/--+                            +3.3V | 0x77 |      +3,3V |       | GND
                VCC 3,3V |      | GND                      <-> SDA |      |   --> MOSI |    NSS| RFM_NSS <--
    int.LED (D0) GPIO 16 |      | GPIO 1  (D10)            --> SCL |      |   <-- MISO |   DIO0| RFM_IRQ -->
    <-- SCL (D1)  GPIO 5 |      | GPIO 3  (D9)                 GND |      |   --> SCK  |       |
           RESET     RST |      | GPIO 15 (D8) RFM_NSS -->         +------+            +-------+
    <-> SDA (D2)  GPIO 4 |      | GPIO 13 (D7) MOSI -->             BH1750            SH1106 OLED
            (D3)  GPIO 0 |      | GPIO 12 (D6) MISO <--            +------+            +-------+
--> RFM_IRQ (D4)  GPIO 2 |      | GPIO 14 (D5) SCK  -->      +3.3V | 0x23 |      +3.3V | 0x3C  |
                         +------+                              GND |      |        GND |       |
                                                           --> SCL |      |    --> SCL |       |
                                                           <-> SDA |      |    <-> SDA |       |
                                                                   +------+            +-------+                      */

/**********************************************************************************************************************/
/* functions */
/**********************************************************************************************************************/

void SetDebugMode(boolean mode)
{
  DEBUG = mode;
  rfm.SetDebugMode(mode);
  crc8.SetDebugMode(mode);
}

// blink led
static void blink (byte pin, byte n = 3, int del = 50)
{
  for (byte i = 0; i < n; ++i) {
    digitalWrite(pin, LOW);
    delay(del);
    digitalWrite(pin, HIGH);
    delay(del);
  }
}

/*!
 * @brief           Write operation in either Wire or SPI
 *
 * param[in]        dev_addr        Wire or SPI device address
 * param[in]        reg_addr        register address
 * param[in]        reg_data_ptr    pointer to the data to be written
 * param[in]        data_len        number of bytes to be written
 *
 * @return          result of the bus communication function
 */
int8_t bus_write(uint8_t dev_addr, uint8_t reg_addr, uint8_t *reg_data_ptr, uint16_t data_len)
{
    Wire.beginTransmission(dev_addr);
    Wire.write(reg_addr);    /* Set register address to start writing to */
 
    /* Write the data */
    for (int index = 0; index < data_len; index++) {
        Wire.write(reg_data_ptr[index]);
    }
 
    return (int8_t)Wire.endTransmission();
}

/*!
 * @brief           Read operation in either Wire or SPI
 *
 * param[in]        dev_addr        Wire or SPI device address
 * param[in]        reg_addr        register address
 * param[out]       reg_data_ptr    pointer to the memory to be used to store the read data
 * param[in]        data_len        number of bytes to be read
 *
 * @return          result of the bus communication function
 */
int8_t bus_read(uint8_t dev_addr, uint8_t reg_addr, uint8_t *reg_data_ptr, uint16_t data_len)
{
    int8_t comResult = 0;
    Wire.beginTransmission(dev_addr);
    Wire.write(reg_addr);                    /* Set register address to start reading from */
    comResult = Wire.endTransmission();
 
    delayMicroseconds(150);                 /* Precautionary response delay */
    Wire.requestFrom(dev_addr, (uint8_t)data_len);    /* Request data */
 
    int index = 0;
    while (Wire.available())  /* The slave device may send less than requested (burst read) */
    {
        reg_data_ptr[index] = Wire.read();
        index++;
    }
 
    return comResult;
}

/*!
 * @brief           System specific implementation of sleep function
 *
 * @param[in]       t_ms    time in milliseconds
 *
 * @return          none
 */
void sleep(uint32_t t_ms)
{
    delay(t_ms);
}

/*!
 * @brief           Capture the system time in microseconds
 *
 * @return          system_current_time    current system timestamp in microseconds
 */
int64_t get_timestamp_us()
{
    return (int64_t) millis() * 1000;
}

    /*!
 * @brief           Load previous library state from non-volatile memory
 *
 * @param[in,out]   state_buffer    buffer to hold the loaded state string
 * @param[in]       n_buffer        size of the allocated state buffer
 *
 * @return          number of bytes copied to state_buffer
 */
uint32_t state_load(uint8_t *state_buffer, uint32_t n_buffer)
{
    // ...
    // Load a previous library state from non-volatile memory, if available.
    //
    // Return zero if loading was unsuccessful or no state was available, 
    // otherwise return length of loaded state string.
    // ...
    return 0;
}

/*!
 * @brief           Save library state to non-volatile memory
 *
 * @param[in]       state_buffer    buffer holding the state to be stored
 * @param[in]       length          length of the state string to be stored
 *
 * @return          none
 */
void state_save(const uint8_t *state_buffer, uint32_t length)
{
    // ...
    // Save the string some form of non-volatile memory, if possible.
    // ...
}

/*!
 * @brief           Load library config from non-volatile memory
 *
 * @param[in,out]   config_buffer    buffer to hold the loaded state string
 * @param[in]       n_buffer        size of the allocated state buffer
 *
 * @return          number of bytes copied to config_buffer
 */
uint32_t config_load(uint8_t *config_buffer, uint32_t n_buffer)
{
    // ...
    // Load a library config from non-volatile memory, if available.
    //
    // Return zero if loading was unsuccessful or no config was available, 
    // otherwise return length of loaded config string.
    // ...
    return 0;
}

/*!
 * @brief           Handling of the ready outputs
 *
 * @param[in]       timestamp       time in nanoseconds
 * @param[in]       iaq             IAQ signal
 * @param[in]       iaq_accuracy    accuracy of IAQ signal
 * @param[in]       temperature     temperature signal
 * @param[in]       humidity        humidity signal
 * @param[in]       pressure        pressure signal
 * @param[in]       raw_temperature raw temperature signal
 * @param[in]       raw_humidity    raw humidity signal
 * @param[in]       gas             raw gas sensor signal
 * @param[in]       bsec_status     value returned by the bsec_do_steps() call
 *
 * @return          none
 */
void output_ready(int64_t timestamp, float iaq, uint8_t iaq_accuracy, float temperature, float humidity,
     float pressure, float raw_temperature, float raw_humidity, float gas, bsec_library_return_t bsec_status)
{
      /* get battery voltage */
      int vcc = analogRead(A0);
      
      /* get light level */
      float lux = bh1750.readLightLevel();
      
      if (OLED) {
        display.setCursor(0,0);
        display.clearDisplay();
      }
      
      /* Output measurings */ 
      Serial.print("[");
      Serial.print(timestamp/1e6);
      Serial.print("] P: ");
      Serial.print(pressure/100);
      Serial.print("| T: ");
      Serial.print(temperature);
      Serial.print("| rH: ");
      Serial.print(humidity);
      Serial.print("| IAQ: ");
      Serial.print(iaq);
      Serial.print(" (");
      Serial.print(iaq_accuracy);
      Serial.print(")");
      Serial.print("| Gas: ");
      Serial.print(gas);
      Serial.print("| UBat: ");
      Serial.print((float) vcc/100);
      Serial.print("V");
      Serial.print("| Light: ");
      Serial.print(lux);
      Serial.println("lx");
      
      /* on OLED Display */
      if (OLED); {
        display.setTextSize(2);
        display.print(temperature,1);
        display.setTextSize(1);
        display.setCursor(48,7);
        display.write(247);
        display.print("C ");

        display.setTextSize(2);
        display.setCursor(74,0);
        display.print((uint8_t)humidity);
        display.setTextSize(1);
        display.setCursor(98,7);
        display.println("%");
        display.println("");
        display.print("Luftdruck: ");
        display.print((int)pressure / 100);
        display.println("hPa");
        
        display.print("Luftguete: ");
        if ( iaq > 0 ) { 
          display.print((uint8_t)iaq);
          display.print(" (");
          display.print(iaq_accuracy);
          display.print(")");
        } else {
          display.print("berechne..");
        }
        display.println("");
        
        display.print("Licht    : ");
        display.print((int)lux);
        display.println("lx");
        
        display.print("Batterie : ");
        display.print((float) vcc/100,1);
        display.println("V");
        
        display.print("Gas: ");
        display.print(gas);
        display.println("Ohm");
        display.display();
      }

      loop_counter += 1; 
      if ( loop_counter >= loop_count_lim ) {  //send every (loop_count_lim * 3) seconds 
        loop_counter = 0;
        /*Encode Payload CustomSensor */
        /* sendarray */
        byte bytes[17];

        /*clear buffer */
        for (uint8_t i = 0; i < sizeof(bytes); i++) { bytes[i] = 0; }
        
        /* FIX "CC" FOR CUSTOM SENSOR */
        bytes[0] = 0xCC;
        
        /* NODE-ID */
        bytes[1] = NODEID;
        
        /* NR. OF DATABYTES */
        bytes[2] = sizeof(bytes) - 4;
        
        /*** TEMPERATURE ***/
        int temp_int = (temperature + 40.05) * 10.0; //+40 to get positive values, +0.05 to round, *10 for 3 digits
        bytes[3]  = temp_int / 100;                  //100's
        bytes[4]  = ((temp_int % 100) / 10) << 4;    //is ok, values are in range of 150 - 900 ;o)
        bytes[4] |= temp_int % 10;
        
        /*** HUMIDITY ***/
        uint8_t hum_int = humidity + 0.5;            //rounded
        bytes[5] = hum_int;
        
        /*** PRESSURE ***/
        int pre_int = (pressure / 100.0) + 0.5;      //pressure rounded in hPa without komma
        
        if ( pre_int > 999 && pre_int < 10000 ) { 
          bytes[6]  = (pre_int / 1000) << 4;         //1000's 
          bytes[6] |= (pre_int / 100) % 10;          //100's 
          bytes[7]  = ((pre_int % 100) / 10) << 4;   //10's
          bytes[7] |= pre_int % 10;                  //1's
        }
        else if ( pre_int > 99 && pre_int < 1000 ) {
          bytes[6]  = pre_int / 100;                 //100's
          bytes[7]  = ((pre_int % 100) / 10) << 4;   //10's
          bytes[7] |= pre_int % 10;                  //1's
        }
        else if ( pre_int > 9 && pre_int < 100 ) {
          bytes[7]  = (pre_int / 10) << 4;           //10's
          bytes[7] |= pre_int % 10;                  //1's
        }
        else if ( pre_int < 10 ) {
          bytes[7] = pre_int;                        //1's
        }
         
        /*** IAQ ***/
        int iaq_int = iaq + 0.5;                     //rounded

        if ( iaq_int > 99 && iaq_int < 501 ) {       // because iaq max. is 500
          bytes[8]  = iaq_int / 100;                 //100's
          bytes[9]  = ((iaq_int % 100) / 10) << 4;   //10's
          bytes[9] |= iaq_int % 10;                  //1's
        }
        else if ( iaq_int > 9 && iaq_int < 100 ) {
          bytes[9]  = (iaq_int / 10) << 4;           //10's
          bytes[9] |= iaq_int % 10;                  //1's
        }
        else if ( iaq_int < 10 ) {
          bytes[9] = iaq_int;                        //1's
        }
        
        /*** LIGHTLEVEL ***/
        int lux_int = lux + 0.5;                     //rounded

        if ( lux_int > 999 && lux_int < 10000 ) { 
          bytes[10]  = (lux_int / 1000) << 4;        //1000's 
          bytes[10] |= (lux_int / 100) % 10;         //100's 
          bytes[11]  = ((lux_int % 100) / 10) << 4;  //10's
          bytes[11] |= lux_int % 10;                 //1's
        }
        else if ( lux_int > 99 && lux_int < 1000 ) {
          bytes[10]  = lux_int / 100;                //100's
          bytes[11]  = ((lux_int % 100) / 10) << 4;  //10's
          bytes[11] |= lux_int % 10;                 //1's
        }
        else if ( lux_int > 9 && lux_int < 100 ) {
          bytes[11]  = (lux_int / 10) << 4;          //10's
          bytes[11] |= lux_int % 10;                 //1's
        }
        else if (lux_int < 10) {
          bytes[11] = lux_int;                       //1's
        }
                
        /*** BATTERY VOLTAGE ***/
        bytes[12] = (vcc + 5) / 10;                  //vcc = 330mV at 3,3V
        
        /*** GAS RESISTANCE ***/
        uint32_t gas_int = gas;
        
        if ( gas_int > 99999 && gas_int < 1000000 ) {
          bytes[13]  = (gas_int / 100000) << 4;          //100000's 
          bytes[13] |= (gas_int / 10000) % 10;           //10000's
          bytes[14]  = ((gas_int / 1000) % 10) << 4;     //1000's
          bytes[14] |= (gas_int / 100) % 10;             //100's
          bytes[15]  = (gas_int % 10) << 4;              //10's
          bytes[15] |= (gas_int * 10) % 10;              //1's
        }        
        else if ( gas_int > 9999 && gas_int < 100000 ) {
          bytes[13]  = gas_int / 10000;                  //10000's
          bytes[14]  = ((gas_int / 1000) % 10) << 4;     //1000's
          bytes[14] |= (gas_int / 100) % 10;             //100's
          bytes[15]  = ((gas_int / 10) % 10) << 4;       //10's
          bytes[15] |= gas_int % 10;                     //1's
        }
        else if ( gas_int > 999 && gas_int < 10000 ) {
          bytes[14]  = (gas_int / 1000) << 4;            //1000's
          bytes[14] |= (gas_int / 100) % 10;             //100's
          bytes[15]  = ((gas_int / 10) % 10) << 4;       //10's
          bytes[15] |= gas_int % 10;                     //1's
        }
        else if ( gas_int > 99 && gas_int < 1000 ) {
          bytes[14]  = gas_int / 100;                    //100's
          bytes[15]  = ((gas_int / 10) % 10) << 4;       //10's
          bytes[15] |= gas_int % 10;                     //1's
        }
        else if ( gas_int > 9 && gas_int < 100 ) {
          bytes[15]  = (gas_int / 10) << 4;              //10's
          bytes[15] |= gas_int % 10;                     //1's
        }
        else if ( gas_int < 10 ) {
          bytes[15] = gas_int;                           //1's
        }
        
        /* CRC BYTE */
        uint8_t crc = crc8.CalculateCRC(bytes, sizeof(bytes) - 1);
        bytes[16] = crc; // CRC over payload bytes 0-15

      rfm.SendArray( (uint8_t *)bytes, sizeof(bytes) );
      rfm.PowerDown();
      if (DEBUG) {
        Serial.print(sizeof(bytes));
        Serial.print(" bytes");
        Serial.println(" sent.");
        Serial.println("");
      }
      blink(LEDpin,1,25);
      }
}

/*!
 * @brief       Main function which configures BSEC library and then reads and processes the data from sensor based
 *              on timer ticks
 *
 * @return      result of the processing
 */
void setup()
{
    return_values_init ret;
    
    pinMode(LEDpin, OUTPUT);
    digitalWrite(LEDpin, HIGH); //LED is low activ !

    SetDebugMode(DEBUG);
    
    Serial.begin(115200);
    sleep(3000); //some time to open the terminalprogram ;o)
    
    Serial.println("");
    Serial.print("BME680 wireless sensor ");
    Serial.println(VERSION);
    Serial.println("Protocol: CustomSensor");
    Serial.print("NODE-ID : ");
    Serial.println(NODEID);
        
    /* init I2C */
    Wire.begin();
    Wire.setClock(400000); 
    
    if (OLED) {
      /* init OLED */
      display.begin(SSD1306_SWITCHCAPVCC, OLED_ADR); // initialize VCC state, I2C addr 0x3C / 0x3D
      display.setTextSize(2);
      display.setTextColor(WHITE);
      display.setCursor(5,4);
      display.clearDisplay();

      /* startscreen */
      display.println("  BME680");
      display.display();
      display.setTextSize(1);
      display.println("");
      display.print("Wireless Sensor ");
      display.println(VERSION);
      display.println("CustomSensor Protocol");
      display.print("Node-ID: ");
      display.println(NODEID);
      display.display();
      sleep(8000);
      display.ssd1306_command(SSD1306_SETCONTRAST);
      display.ssd1306_command(0x96);
      display.setCursor(0,0);
      display.clearDisplay();
    }
    
    /* --- RFM69CW init --- */
    SPI.begin();
    rfm.Begin();
    if ( rfm.IsConnected() ) {

      if (OLED) {
        display.println("RFM69 gefunden.");
        display.display();
      }
      Serial.println("RFM69 found.");
            
      rfm.InitializeLaCrosse();
      if (DEBUG) {
        Serial.println("Init LaCrosse ok.");
      }
      
      rfm.SetFrequency(INITIAL_FREQ);
      float init_freq = float(INITIAL_FREQ);
      display.print("Frequenz: ");
      display.print(init_freq/1000,3);
      display.println(" MHz");
      if (DEBUG) {
        Serial.print("Set frequency to ");
        Serial.print(init_freq/1000,3);
        Serial.println(" MHz.");
      }
      
      rfm.SetDataRate(DATA_RATE);
      if (OLED) {
        display.print("Baudrate: ");
        display.print(DATA_RATE);
        display.println(" Baud");
      }
      if (DEBUG) {
        Serial.print("Set datarate to ");
        Serial.print(DATA_RATE);
        Serial.println(" bps.");
      }
      
      rfm.PowerDown(); // sleep to save power
      Serial.println("RFM69 initialized.");
      if (OLED) {
        display.println("RFM69 bereit.");
        //display.println("");
        display.display();
      }
    }
    else {
      if (OLED) {
        display.print("RFM69 Fehler, Stop!");
        display.display();
      }
      Serial.print("RFM69 failed, stop!");
      blink(LEDpin, 6, 500);
      for (;;); // spin fo rever
    }
    
    /* --- BME680 init --- */
    Serial.print("BME680: try init on 0x");
    Serial.print(BME680_I2C_ADDR, HEX);
    Serial.print(" ... ");
    if (OLED) {
      display.print("BME680 init auf 0x");
      display.println(BME680_I2C_ADDR, HEX);
      display.display();
    }
    
    /* Call to the function which initializes the BSEC library 
     * Switch on low-power mode and provide no temperature offset */
    ret = bsec_iot_init(BSEC_SAMPLE_RATE_LP, TEMP_CORR, bus_write, bus_read, sleep, state_load, config_load, BME680_I2C_ADDR);
    
    if (ret.bme680_status)
    {
        /* Could not intialize BME680 */
        Serial.println("Error while initializing, wrong I2C addr ?");
        if (OLED) {
          display.println("BME680 init. Fehler!");
          display.println("Falsche I2C Adresse?");
          display.display();
        }
        return;
    }
    else if (ret.bsec_status)
    {
        /* Could not intialize BSEC library */
        Serial.println("Error while initializing BSEC library !");
        if (OLED) {
          display.println("BSEC init. Fehler !");
          display.display();
        }
        return;
    }
    
    Serial.println("done.");
    if (OLED) {
      display.println("bereit");
      display.display();
    }
    
    /* --- BH1750 init --- */
    Serial.print("BH1750 init ... ");
    if (OLED) {
      display.print("BH1750 init...");
      display.display();
    }
    /* for normal sensor resolution (1 lx resolution, 0-65535 lx, 120ms, no PowerDown) use: bh1750.begin(RESOLUTION_NORMAL, false); */
    if ( bh1750.begin() ) {
      Serial.println("done.");
      Serial.println("Ready, start measuring ...");
      Serial.println("");
      if (OLED) {
        display.println("bereit");
        sleep(6000);
        display.print("Starte Messungen ...");
        display.display();
        sleep(2000);
      }
    } else {
      if (OLED) {
        display.println("");
        display.print("BH1750 Fehler, Stop!");
        display.display();
      }
      Serial.print("BH1750 failed, stop!");
      blink(LEDpin, 6, 500);
      for (;;); // spin fo rever
    }
    
    loop_counter = loop_count_lim; //to send data at startup
    
    /* Call to endless loop function which reads and processes data based on sensor settings */
    /* State is saved every 10.000 samples, which means every 10.000 * 3 secs = 500 minutes  */
    bsec_iot_loop(sleep, get_timestamp_us, output_ready, state_save, 10000);
}

void loop()
{
}

/*
 * Copyright (C) 2017 Robert Bosch. All Rights Reserved. 
 *
 * Disclaimer
 *
 * Common:
 * Bosch Sensortec products are developed for the consumer goods industry. They may only be used
 * within the parameters of the respective valid product data sheet.  Bosch Sensortec products are
 * provided with the express understanding that there is no warranty of fitness for a particular purpose.
 * They are not fit for use in life-sustaining, safety or security sensitive systems or any system or device
 * that may lead to bodily harm or property damage if the system or device malfunctions. In addition,
 * Bosch Sensortec products are not fit for use in products which interact with motor vehicle systems.
 * The resale and/or use of products are at the purchasers own risk and his own responsibility. The
 * examination of fitness for the intended use is the sole responsibility of the Purchaser.
 *
 * The purchaser shall indemnify Bosch Sensortec from all third party claims, including any claims for
 * incidental, or consequential damages, arising from any product use not covered by the parameters of
 * the respective valid product data sheet or not approved by Bosch Sensortec and reimburse Bosch
 * Sensortec for all costs in connection with such claims.
 *
 * The purchaser must monitor the market for the purchased products, particularly with regard to
 * product safety and inform Bosch Sensortec without delay of all security relevant incidents.
 *
 * Engineering Samples are marked with an asterisk (*) or (e). Samples may vary from the valid
 * technical specifications of the product series. They are therefore not intended or fit for resale to third
 * parties or for use in end products. Their sole purpose is internal client testing. The testing of an
 * engineering sample may in no way replace the testing of a product series. Bosch Sensortec
 * assumes no liability for the use of engineering samples. By accepting the engineering samples, the
 * Purchaser agrees to indemnify Bosch Sensortec from all claims arising from the use of engineering
 * samples.
 *
 * Special:
 * This software module (hereinafter called "Software") and any information on application-sheets
 * (hereinafter called "Information") is provided free of charge for the sole purpose to support your
 * application work. The Software and Information is subject to the following terms and conditions:
 *
 * The Software is specifically designed for the exclusive use for Bosch Sensortec products by
 * personnel who have special experience and training. Do not use this Software if you do not have the
 * proper experience or training.
 *
 * This Software package is provided `` as is `` and without any expressed or implied warranties,
 * including without limitation, the implied warranties of merchantability and fitness for a particular
 * purpose.
 *
 * Bosch Sensortec and their representatives and agents deny any liability for the functional impairment
 * of this Software in terms of fitness, performance and safety. Bosch Sensortec and their
 * representatives and agents shall not be liable for any direct or indirect damages or injury, except as
 * otherwise stipulated in mandatory applicable law.
 *
 * The Information provided is believed to be accurate and reliable. Bosch Sensortec assumes no
 * responsibility for the consequences of use of such Information nor for any infringement of patents or
 * other rights of third parties which may result from its use. No license is granted by implication or
 * otherwise under any patent or patent rights of Bosch. Specifications mentioned in the Information are
 * subject to change without notice.
 *
 * It is not allowed to deliver the source code of the Software to any third party without permission of
 * Bosch Sensortec.
 *
 */


/*! @}*/
